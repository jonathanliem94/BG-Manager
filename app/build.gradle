apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'dagger.hilt.android.plugin'
apply plugin: 'androidx.navigation.safeargs.kotlin'

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    def versionPropsFile = file('version.properties')
    def increment = 0
    def runTasks = gradle.startParameter.taskNames
    def mVersionName = ""
    def mFileName = ""

    Properties versionProps = new Properties()
    if (!versionPropsFile.exists()) {
        versionProps['VERSION_MAJOR'] = "0"
        versionProps['VERSION_MINOR'] = "1"
        versionProps['VERSION_PATCH'] = "0"
        versionProps['VERSION_BUILD'] = "0"
        versionProps['VERSION_CODE'] = "1"
        versionProps.store(versionPropsFile.newWriter(), null)
    }

    if ('assembleRelease' in runTasks) {
        increment = 1
    }

    if (versionPropsFile.canRead()) {
        versionProps.load(new FileInputStream(versionPropsFile))

        versionProps['VERSION_PATCH'] = (versionProps['VERSION_PATCH'].toInteger() + increment).toString()
        versionProps['VERSION_CODE'] = (versionProps['VERSION_CODE'].toInteger() + increment).toString()
        versionProps['VERSION_BUILD'] = (versionProps['VERSION_BUILD'].toInteger() + 1).toString()

        // store new version numbers
        versionProps.store(versionPropsFile.newWriter(), null)

        mVersionName = "v${versionProps['VERSION_MAJOR']}.${versionProps['VERSION_MINOR']}.${versionProps['VERSION_PATCH']}"
        mFileName = "bgmanager-${mVersionName}.apk"

        defaultConfig {
            applicationId 'com.jonathanl.bgmanager'
            minSdkVersion 23
            targetSdkVersion 30
            versionCode versionProps['VERSION_CODE'].toInteger()
            versionName "${mVersionName} Build: ${versionProps['VERSION_BUILD']}"
        }

    } else {
        throw new FileNotFoundException("Could not read version.properties!")
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            if (output.outputFile != null && output.outputFile.name.endsWith('.apk')) {
                outputFileName = mFileName
            }
        }
    }

    defaultConfig {
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        kapt {
            arguments {
                arg("room.schemaLocation", "$projectDir/schemas".toString())
            }
        }
    }
    buildFeatures{
        viewBinding = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation"org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.4.31"
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.fragment:fragment-ktx:1.3.5'
    implementation 'androidx.room:room-runtime:2.3.0'
    kapt 'androidx.room:room-compiler:2.3.0'
    implementation 'androidx.room:room-rxjava2:2.3.0'
    implementation 'com.github.bumptech.glide:glide:4.11.0'

    // Material Design
    implementation 'com.google.android.material:material:1.4.0'

    // Hilt
    implementation 'com.google.dagger:hilt-android:2.38'
    kapt 'com.google.dagger:hilt-android-compiler:2.38'

    // navigation dependencies
    implementation 'androidx.navigation:navigation-fragment-ktx:2.3.5'
    implementation 'androidx.navigation:navigation-ui-ktx:2.3.5'

    // Network/API dependencies
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-simplexml:2.7.1'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.7.1'

    // RX dependencies
    implementation 'io.reactivex.rxjava2:rxjava:2.2.17'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.4.0'
    implementation "io.reactivex.rxjava2:rxandroid:2.1.1"

    // Test Dependencies
    testImplementation 'junit:junit:4.13.2'
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0"

    // UI Test Dependencies
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test:rules:1.4.0'
    androidTestImplementation 'androidx.fragment:fragment-testing:1.3.5'
    // Just for the library DrawerActions
    androidTestImplementation 'com.android.support.test.espresso:espresso-contrib:3.0.2'

}
